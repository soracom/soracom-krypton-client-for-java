plugins {
    id 'org.ajoberstar.git-publish' version '1.0.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'


repositories {
	mavenLocal()
	mavenCentral()
    jcenter()
}

group = 'io.soracom'
mainClassName = 'io.soracom.krypton.KryptonClient'
archivesBaseName = rootProject.name
version = '0.0.4-SNAPSHOT' 

sourceCompatibility = 1.7
targetCompatibility = 1.7
    
dependencies {
   // Java Simple Serial Connector 
    compile 'org.scream3r:jssc:2.8.0'
    compile 'com.google.code.gson:gson:2.8.2'
    testCompile 'junit:junit:4.12'
}

task fatJar(type: Jar) {
	manifest {
	    attributes 'Implementation-Title': archivesBaseName,
					'Implementation-Version': version,
					'Main-Class': 'io.soracom.krypton.KryptonClient'
    }
    baseName = archivesBaseName + "-app"
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
  classifier "sources"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
    	maven {
	        url file("$buildDir/dist-mvn")
   		}
   	}
}

gitPublish {
    repoUri = 'https://github.com/soracom/maven-repository.git'
    branch = 'gh-pages'
    repoDir = file("maven-repository")
    contents {
        from file("soracom-krypton-client-for-java/build/dist-mvn")
    }
    preserve {
        include '**'
    }
    commitMessage = 'Publishing a new archive'
}

task preparePush {
    subprojects.each { dependsOn("${it.name}:clean") }
    subprojects.each { dependsOn("${it.name}:build") }
    subprojects.each { dependsOn("${it.name}:publish") }
}
gitPublishPush.dependsOn preparePush

build.dependsOn fatJar